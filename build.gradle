buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}

group = 'org.opencv'
version = '4.3.0'

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def openCVersionName = "4.3.0"
def openCVersionCode = ((4 * 100 + 3) * 100 + 0) * 10 + 0

println "OpenCV: " +openCVersionName + " " + project.buildscript.sourceFile

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28

        versionCode openCVersionCode
        versionName openCVersionName

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                targets "opencv_jni_shared"
            }
        }
    }
    buildTypes {
        debug {
            packagingOptions {
                doNotStrip '**/*.so'  // controlled by OpenCV CMake scripts
            }
        }
        release {
            packagingOptions {
                doNotStrip '**/*.so'  // controlled by OpenCV CMake scripts
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            manifest.srcFile 'java/AndroidManifest.xml'
            java.srcDirs = ['java/src']
            renderscript.srcDirs = ['java/src']
            res.srcDirs = ['java/res']
            aidl.srcDirs = ['java/src/org/opencv/engine']
            jniLibs.srcDirs = ['native/jniLibs']
        }
    }
    externalNativeBuild {
        cmake {
            path (project.projectDir.toString() + '/libcxx_helper/CMakeLists.txt')
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_API_KEY')
    override = true
    configurations = ['archives']
    pkg {
        userOrg = "homodeusio"
        repo = "opencv-aruco-android"
        name = "opencv-aruco-android"
        licenses = ["BSD 3-Clause"]
        publish = true
        version {
            name = '4.3.0'
        }
        labels = ['aar', 'android']
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging = 'aar'
                name = 'OpenCV-Android'
                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    api 'com.android.support:appcompat-v7:28.0.0'
}
